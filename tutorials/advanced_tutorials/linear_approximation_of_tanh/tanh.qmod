// Linear Approximation of Hyperbolic Tangent Function

// This example demonstrates the computation of the function tanh(x) using
// piecewise linear approximation.


// This function outputs the segment label of a given x value
qperm map_label(num_segs: int, const v: qbit[], output label: qbit[]) {
  label = v[v.len-num_segs:v.len];  // copy MSBs of v
}

qfunc compute_piecewise_linear(As: real[], Bs: real[], x: qnum, y: qnum) {
  label: qnum;
  map_label(log(As.len,2), x, label);

  repeat (i: As.len) {
    control (label==i) {
      y ^= As[i]*x + Bs[i];  // the linear function for segment i
    }
  }

  // Note: Automatic uncomputation of 'label' happens here
}

qfunc main(output x: qnum, output y: qnum) {
  allocate(4, false, 4, x);
  hadamard_transform(x);
  allocate(4, false, 4, y);
  compute_piecewise_linear( // tanh coefficients for 4 subdomains
    [1.0, 0.9375, 0.8125, 0.625  ],
    [0.0, 0.03125, 0.0625, 0.21875],
  x, y);
}
