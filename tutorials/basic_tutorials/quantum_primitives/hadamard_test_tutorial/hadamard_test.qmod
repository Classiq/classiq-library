// Hadamard Test

// Implements a minimal version of the Hadamard test,
// where an ancilla qubit controls the application of a Quantum
// Fourier Transform (QFT) on a target register. Measuring the ancilla
// allows extracting information about the real part of an expectation
// value associated with the controlled unitary.

// Utility function, applying a controlled QFT
qfunc controlled_qft(expectation_value: qbit, psi: qbit[]) {
  control (expectation_value) {
    qft(psi);
  }
}


// Performs the Hadamard test utilizing the within-applied function implementing
// U-dagger V U, where U is the Hadamard gate and V is the controlled_qft function.
qfunc preparation_and_application(expectation_value: qbit, psi: qbit[]) {
  within {
    H(expectation_value);  // Creates a superposition of |0> and |1> states
  } apply {
    controlled_qft(expectation_value, psi);
  }
}


qfunc main(output expectation_value: qbit) {
  psi: qbit[4]; // Target quantum variable, of size 4
  allocate(expectation_value); // Ancilla qubit used for the Hadamard test
  allocate(psi);    // Initializes the target state

  preparation_and_application(expectation_value, psi);
  drop(psi);        // Indicates that the quantum state should not be measured
}
