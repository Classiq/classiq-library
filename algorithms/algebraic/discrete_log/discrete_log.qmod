qfunc modular_exponentiation(N: int, a: int, x: qbit[], pw: qbit[]) {
  repeat (index: pw.len) {
    control (pw[index]) {
      modular_multiply_constant_inplace(N, a ** (2 ** index), x);
    }
  }
}

qfunc discrete_log_oracle(g_generator: int, x_element: int, N_modulus: int, alpha: qbit[], beta: qbit[], output func_res: qnum) {
  allocate(ceiling(log(N_modulus, 2)), func_res);
  func_res ^= 1;
  modular_exponentiation(N_modulus, x_element, func_res, alpha);
  modular_exponentiation(N_modulus, g_generator, func_res, beta);
}

qfunc discrete_log(g: int, x: int, N: int, order: int, output alpha: qbit[], output beta: qbit[], output func_res: qbit[]) {
  allocate(ceiling(log(order, 2)), alpha);
  allocate(ceiling(log(order, 2)), beta);
  hadamard_transform(alpha);
  hadamard_transform(beta);
  discrete_log_oracle(g, x, N, alpha, beta, func_res);
  invert {
    qft(alpha);
  }
  invert {
    qft(beta);
  }
}

qfunc main(output alpha: qnum, output beta: qnum, output func_res: qnum) {
  discrete_log(3, 2, 5, 4, alpha, beta, func_res);
}
