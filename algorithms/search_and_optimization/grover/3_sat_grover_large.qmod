// Solving 3-SAT Problem using Grover's Algorithm

// We demonstrate Groverâ€™s search algorithm by applying it to a simple instance
// of the 3-SAT problem, showing how quantum amplitude amplification can be
// used to find a satisfying assignment.

qperm sat3_oracle(const x: qbit[]) {
    control (
        (   x[1] |   x[2] |   x[3]) &
        (  ~x[0] |   x[1] |   x[2]) &
        (  ~x[0] |   x[1] |  ~x[2]) &
        (  ~x[0] |  ~x[1] |   x[2]) &
        (   x[0] |  ~x[1] |  ~x[2]) &
        (   x[0] |  ~x[1] |   x[2]) &
        (  ~x[0] |  ~x[1] |  ~x[3]) &
        (  ~x[0] |  ~x[1] |   x[3]) &
        (  ~x[1] |  ~x[2] |  ~x[3]) &
        (   x[1] |  ~x[2] |   x[3]) &
        (   x[0] |  ~x[2] |   x[3]) &
        (   x[0] |  ~x[1] |  ~x[3]) &
        (  ~x[0] |  ~x[1] |  ~x[2])
    ) {
       phase(pi);  // flip the sign on states that satisfy the 3-SAT clauses
    }
}

qfunc main(r: int, output x: qbit[4]) {
  allocate(x);
  hadamard_transform(x);
  power (r) {
    grover_operator(lambda(vars) {
      sat3_oracle(vars);
    }, hadamard_transform, x);
  }
}
