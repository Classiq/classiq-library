qfunc my_sp(x: qnum, y: qnum) {
  hadamard_transform(x);
  hadamard_transform(y);
}

qfunc my_grover_operator(oracle_operand: qfunc (), diffuser_operand: qfunc ()) {
  oracle_operand();
  diffuser_operand();
}

qfunc prep_minus(out: qbit) {
  X(out);
  H(out);
}

qfunc my_oracle(predicate: qfunc (qbit)) {
  aux: qbit;
  allocate(aux);
  within {
    prep_minus(aux);
  } apply {
    predicate(aux);
  }
  free(aux);
}

qperm my_predicate(const x: qnum, const y: qnum, res: qbit) {
  res ^= ((x + y) < 9) and (((x * y) % 4) == 1);
}

qperm zero_predicate(const x: qnum, const y: qnum, res: qbit) {
  temp: qnum;
  within {
    {x, y} -> temp;
  } apply {
    res ^= temp == 0;
  }
}

qfunc my_diffuser(sp_operand: qfunc (qnum, qnum), x: qnum, y: qnum) {
  within {
    invert {
      sp_operand(x, y);
    }
  } apply {
    my_oracle(lambda(q) {
      zero_predicate(x, y, q);
    });
  }
}

qfunc main(output x: qnum, output y: qnum) {
  allocate(6, x);
  allocate(6, y);
  my_sp(x, y);
  power (4) {
    my_grover_operator(lambda() {
      my_oracle(lambda(q) {
        my_predicate(x, y, q);
      });
    }, lambda() {
      my_diffuser(lambda(x, y) {
        my_sp(x, y);
      }, x, y);
    });
  }
}
